% PT Scanner Version 5.0
%
% Date:  30 Jun 1980
%       (Revised v5.0 - 10 Dec 2020)
%
% Author:  Alan Rosselet
%
% Abstract
%     The scanner breaks a PT source program up into syntax tokens.
%     It removes token separators - blanks, tabs, comments.  The
%     scanner detects these lexical errors:  unclosed comments,
%     unclosed literals, literals that cross a line boundary,
%     illegal characters and integers followed by identifiers with 
%     no separator between them.
   

Input : 
        lLetter 
        firstCharClass = lLetter 
        lDigit
        lPlus           '+'
        lMinus          '-'
        lStar           '*'
        lColon          ':'
        lEquals         '='
        lComma          ','
        lSemicolon      ';'
        lQuote          '"'     % updated existing character class to double quote
        lSlash          '/'     % slash character class added
        lBackslash
        lPercent        '%'     % percent character class added
        lHash           '#'     % hash character class added
        lBang           '!'     % bang character class added
        lBar            '|'     % bar character class added
        lLeftAngle      '<'
        lRightAngle     '>'
        lLeftParen      '('
        lRightParen     ')'
        lLeftBracket    '['
        lRightBracket   ']'
        lLeftBrace      '{'
        lRightBrace     '}'
        lBlank
        lIllegal
        lNewLine
        lCarriageReturn
        lTab
        lNewPage 
        lEndFile
        lastCharClass = lEndFile;

Output :
        % Keyword tokens -
        %   Tokens pDiv through pRepeat are keywords which will be 
        %   separated from user defined identifiers by the Screener.
        %   This Scanner treats them all as identifiers.
        pChoose
        firstKeywordToken = pChoose
        pElseif
        pFun
        pIs
        pLike
        pPkg
        pPublic
        pUsing
        pVal
        pWhen
        % All of the above keyword tokens were added
        pOr
        pAnd
        pNot
        pThen
        pElse
        pOf
        pEnd
        pFile
        pVar
        pIf
        pWhile
        pRepeat
        lastKeywordToken = pRepeat

        % Scanner tokens -
        %   These are the tokens actually recognized by the Scanner itself.

        % Compound tokens - 
        %   These will be sent to the parser with a value or index.
        pIdentifier 
        firstCompoundInputToken = pIdentifier
        pInteger
        pStringLiteral
        lastCompoundInputToken = pStringLiteral

        % Non-compound tokens - 
        %   These tokens simply represent themselves.
        pSlash          %Confused about how to implement
        pPercent        %Done
        pHash           %Done
        pOrBar            %Done
        pDoubleOrBar      %Done
        pPlusEquals     %Done
        pMinusEquals    %Done
        pStarEquals     %Done
        pSlashEquals    %Done
        pPercentEquals  %Done
        pDoubleEquals   %Done
        % All of the above syntax tokens were added
        pNewLine
        pEndFile
        pPlus
        pMinus
        pStar
        pComma
        pSemicolon
        pColon
        pEquals
        pNotEqual
        pLess
        pLessEqual
        pGreaterEqual
        pGreater
        pLeftParen
        pRightParen
        pLeftBracket
        pRightBracket
        lastSyntaxToken = pRightBracket;

Error :
        eIllegalChar = 5
        firstScanError = eIllegalChar
        eStringLiteralLine
        eStringLiteralEOF
        eCommentEOF
        eNoSeparator
        lastScanError = eNoSeparator;

mechanism Buffer :
    % Buffer saves the characters associated with the
    % compound tokens identifier, integer and literal.

    oBufferSave;
        % Save the character form of the last accepted lexical token.


rules

% Note that the order of choice alternatives
% may have a significant effect on performance
% under some implementations of S/SL.
Scan :
        {[
            | lBlank :
            | lLetter:
                oBufferSave
                @Identifier
            | lNewLine:
                .pNewLine
            | lCarriageReturn:
                % from Windows files, ignore
            | lTab:
            | ';':
                .pSemicolon
            | '(':
                .pLeftParen
            | ')':
                .pRightParen
            | '[':
                .pLeftBracket
            | ']':
                .pRightBracket
            | ',':
                .pComma
            | ':':
                .pColon
            | lDigit:
                oBufferSave
                @Integer
                [
                    | lLetter:
                        #eNoSeparator
                        oBufferSave
                        @Identifier
                    | *:
                ]
            | '=':
                [
                    | '=':
                        .pDoubleEquals
                    |*:
                        .pEquals
                ]
            | '+':
                [
                    | '=':
                        .pPlusEquals
                    |*:
                        .pPlus
                ]
            | lQuote:
                @StringLiteral
            | '-':
                [
                    | '=':
                        .pMinusEquals
                    |*:
                        .pMinus
                ]
            | '!':
                [
                    | '=':
                        .pNotEqual
                    |*:
                        .eIllegalChar
                ]
            | '<':
                [
                    | '=':
                        .pLessEqual
                    | *:
                        .pLess
                ]
            | '>':
                [
                    | '=':
                        .pGreaterEqual
                    | *:
                        .pGreater
                ]
            | '/':
                [
                    | '=':
                        .pSlashEquals
                    | '/': 
                        @SingleLineComment
                    | '*':
                        @MultiLineComment
                    | *:
                        .pSlash % need to implement pDiv somewhere
                ]
            | '*':
                [
                    | '=':
                        .pStarEquals
                    |*:
                        .pStar
                ]
            | '%':
                [
                    | '=':
                        .pPercentEquals
                    |*:
                        .pPercent
                ]
            | '#':
                .pHash
            | '|':
                [
                    | '|':
                        .pDoubleOrBar
                    |*:
                        .pOrBar
                ]
            | lNewPage:
            | lEndFile:
                .pEndFile
                >
            | *:
                ?
                #eIllegalChar
        ]};

Identifier :
        % Assemble an identifier token

        {[
            | lLetter, lDigit:
                oBufferSave
            | *:
                .pIdentifier
                >
        ]};

Integer :
        % Assemble an integer token

        {[
            | lDigit:
                oBufferSave
            | *:
                .pInteger
                >
        ]};

StringLiteral :
        % Assemble a literal string token

        {[
            | lQuote:
                [
                    | lQuote:
                        oBufferSave
                    | *:
                        .pStringLiteral
                        >
                ]
            | lNewLine:         % literal must not cross a line boundary
                #eStringLiteralLine
                .pNewLine
                >
            | lEndFile:         % literal must be closed before EOF
                #eStringLiteralEOF
                .pEndFile
                >
            | *:        % literal may contain any other character
                [
                    | lBackslash:
                        oBufferSave
                    | *:
                ]
                ?
                oBufferSave
        ]};

SingleLineComment :
        % Discard a single line comment
        {[
            | lEndFile:         % if the file ends, emit pEndFile and return
                .pEndFile
                >
            | lNewLine:         % if the line ends, emit pNewLine and return
                .pNewLine
                >
            | *:                % all other characters are part of the comment, discard them
                ?
        ]};
MultiLineComment :
        % Discard a multiline comment
        {[
            | '*':
                [
                    | '/':
                        >
                    | *:
                ]
            | lNewLine:
                .pNewLine
            | lEndFile:
                #eCommentEOF
                .pEndFile
                >
            | *:
                ?
        ]};

end
